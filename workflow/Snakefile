
samples = ["SRR10379721", "SRR10379722", "SRR10379723", "SRR10379724", "SRR10379725", "SRR10379726"] #références des accesions 

samples_dev = ["test_21"] #références des accesions 

configfile: "workflow/config.yaml"

localrules: all, download_annotated_ref_genome, download_ref_genome, download_rnaseq # pour améliorer les choses sur un cluster 

rule all:
    input:
        "counts.txt"


# Téléchargement 

rule download_ref_genome:
    output:
        "ref_genome.fna"  # fichier au format FASTA (.fa)
    threads: 1 #la règle utilisera un coeur (car il y a un unique fichier)
    shell:
        "wget -O - https://ftp.ncbi.nlm.nih.gov/genomes/all/GCF/000/013/425/GCF_000013425.1_ASM1342v1/GCF_000013425.1_ASM1342v1_genomic.fna.gz | gzip -d > {output}"

rule download_annotated_ref_genome:
    output:
        "ref_genome.gff"  #annotation au format .gff
    threads: 1 #la règle utilisera un coeur (car il y a un unique fichier)
    shell:
        "wget -O - https://ftp.ncbi.nlm.nih.gov/genomes/all/GCF/000/013/425/GCF_000013425.1_ASM1342v1/GCF_000013425.1_ASM1342v1_genomic.gff.gz | gzip -d > {output}"

rule download_rnaseq:
    output:
        temp("{sample}/{sample}.sra") #permet l'utilisation temporaire puis de supprimer afin de ne pas garder des fichiers lourds et inutiles
    singularity : config["singularity"]["sra_toolkit"]
    resources:
        shell_exec="sh"
    shell:
        "prefetch {wildcards.sample}" 


rule index_genome:
    input:
        "ref_genome.fna"
    output:
        expand("ref_genome.{index_elements}.ebwt",index_elements = ["1","2","3","4","rev.1","rev.2"])
    threads: 1
    singularity: config["singularity"]["bowtie+samtools"]
    resources:
        shell_exec="sh"
    shell:
        "bowtie-build {input} ref_genome"


rule extract_rnaseq: #extract from sra --> fastq : ressource intensive, file size can increase a lot !
    input:
        "{sample}/{sample}.sra"
    output:
        "{sample}.fastq" 
    singularity: config["singularity"]["sra_toolkit"]
    resources:
        shell_exec="sh"
    shell:
        "fasterq-dump --threads=1 {input}" 

rule trim:
    input:
        "{sample}.fastq"
    output:
        "trimmed_{sample}.fastq.gz" 
    resources:
        shell_exec="sh"
    singularity: config["singularity"]["cutadapt"]
    threads: config["cutadapt"]["threads"]
    params:
        config["cutadapt"]["primer"],
        config["cutadapt"]["quality"],
        config["cutadapt"]["m"]
    shell:
        "cutadapt {params[0]} -q {params[1]} -m {params[2]} -o {output} {input}" # adapter sequence found at : https://support-docs.illumina.com/SHARE/AdapterSequences/Content/SHARE/AdapterSeq/TruSeq/UDIndexes.htm
        
rule map:
    input:
        sequence = "trimmed_{sample}.fastq.gz",
        index = expand("ref_genome.{index_elements}.ebwt",index_elements = ["1","2","3","4","rev.1","rev.2"])
    output:
        "{sample}.bam" 
    singularity: config["singularity"]["bowtie+samtools"]
    threads: config['bowtie_mapping_rna']['threads']
    shell:
        "gunzip -c {input.sequence} | bowtie -p {threads} -S ref_genome - | samtools sort -@ {threads} - > {output}"

rule count:
    input:
        annotation = "ref_genome.gff",
        id = expand("{sample}.bam" , sample=samples)
    output:
         "counts.txt","counts.txt.summary"
    singularity: config["singularity"]["subread"]
    params: 
        config["feature_counts"]["feature"],
        config["feature_counts"]["attribute"],
        config["feature_counts"]["strandness"]

    resources:
        shell_exec="sh"
    threads: config["feature_counts"]["threads"]
    shell:
        "featureCounts -T {threads} -t {params[0]} -g {params[1]} -s {params[2]} -a {input.annotation} -o {output[0]} {input.id}"